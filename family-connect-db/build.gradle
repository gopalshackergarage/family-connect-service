plugins {
    id 'org.springframework.boot' version '2.0.0.RELEASE'
    id 'nu.studer.jooq' version '2.0.11'
    id "org.flywaydb.flyway" version "5.2.1"
    id 'java'
}

apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'nu.studer.jooq'

group = 'com.gakshintala.mylabspace'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

ext['jooq.version'] = '3.9.6'

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-jooq')
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    compile('org.postgresql:postgresql:+')
    jooqRuntime 'postgresql:postgresql:+'
}

jooq {
    version='3.9.6'
    family(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:6666/family'
            user = 'admin'
            password = 'admin'
            schema = 'public'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = false
                fluentSetters = true
            }
            target {
                packageName = 'jooq.db.gen'
                directory = 'src/main/generated/java'
            }
        }
    }
}


flyway {
    url = 'jdbc:postgresql://localhost:6666/family'
    user = 'admin'
    password = 'admin'
    schemas = ['public']
    locations = ["filesystem:$project.projectDir/src/main/resources/db/migration"]
}

generateFamilyJooqSchemaSource.dependsOn flywayMigrate
test.dependsOn flywayClean
